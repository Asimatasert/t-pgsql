# Maintainer: Asim Atasert <asimatasert@outlook.com>
pkgname=t-pgsql
pkgver=3.4.0
pkgrel=1
pkgdesc="PostgreSQL database sync, backup and clone tool with notifications"
arch=('any')
url="https://github.com/Asimatasert/t-pgsql"
license=('MIT')
depends=('postgresql' 'curl' 'openssh' 'bash')
optdepends=(
    'zsh: for zsh completion support'
    'bash-completion: for bash completion support'
    'fish: for fish completion support'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Asimatasert/t-pgsql/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('8fe43c12cde366afe992f74ac22877688aac7a81649424a251c94ffafbe0e9c4')

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install main script
    install -Dm755 t-pgsql "${pkgdir}/usr/bin/t-pgsql"

    # Install zsh completion
    install -Dm644 completions/_t-pgsql "${pkgdir}/usr/share/zsh/site-functions/_t-pgsql"

    # Install bash completion
    install -Dm644 completions/t-pgsql.bash "${pkgdir}/usr/share/bash-completion/completions/t-pgsql"

    # Install fish completion
    install -Dm644 completions/t-pgsql.fish "${pkgdir}/usr/share/fish/vendor_completions.d/t-pgsql.fish"

    # Install man page
    install -Dm644 man/t-pgsql.1 "${pkgdir}/usr/share/man/man1/t-pgsql.1"

    # Install license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Install documentation
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -Dm644 jobs.yaml.example "${pkgdir}/usr/share/doc/${pkgname}/jobs.yaml.example"
}
