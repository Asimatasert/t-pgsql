# Maintainer: Asim Atasert <asimatasert@outlook.com>
pkgname=t-pgsql
pkgver=3.7.0
pkgrel=1
pkgdesc="PostgreSQL database sync, backup and clone tool with notifications"
arch=('any')
url="https://github.com/Asimatasert/t-pgsql"
license=('MIT')
depends=('postgresql' 'curl' 'openssh' 'bash')
optdepends=(
    'zsh: for zsh completion support'
    'bash-completion: for bash completion support'
    'fish: for fish completion support'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Asimatasert/t-pgsql/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('b1572b4a243f84b1828305368f080d43dcf073cb4133a85cdab329e0ade9b2b5')

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install main script
    install -Dm755 t-pgsql "${pkgdir}/usr/bin/t-pgsql"

    # Install zsh completion
    install -Dm644 completions/_t-pgsql "${pkgdir}/usr/share/zsh/site-functions/_t-pgsql"

    # Install bash completion
    install -Dm644 completions/t-pgsql.bash "${pkgdir}/usr/share/bash-completion/completions/t-pgsql"

    # Install fish completion
    install -Dm644 completions/t-pgsql.fish "${pkgdir}/usr/share/fish/vendor_completions.d/t-pgsql.fish"

    # Install man page
    install -Dm644 man/t-pgsql.1 "${pkgdir}/usr/share/man/man1/t-pgsql.1"

    # Install license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Install documentation
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -Dm644 jobs.yaml.example "${pkgdir}/usr/share/doc/${pkgname}/jobs.yaml.example"
}
