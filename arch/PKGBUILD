# Maintainer: Asim Atasert <asimatasert@outlook.com>
pkgname=t-pgsql
pkgver=3.3.0
pkgrel=1
pkgdesc="PostgreSQL database sync, backup and clone tool with notifications"
arch=('any')
url="https://github.com/Asimatasert/t-pgsql"
license=('MIT')
depends=('postgresql' 'curl' 'openssh' 'bash')
optdepends=(
    'zsh: for zsh completion support'
    'bash-completion: for bash completion support'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Asimatasert/t-pgsql/archive/refs/tags/v${pkgver}.tar.gz")
sha256sums=('2ec8d0fc1d6f4eb20e0cd68fb91459957c184a8b7fb959b923a6016d7b887ad3')

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Install main script
    install -Dm755 t-pgsql "${pkgdir}/usr/bin/t-pgsql"

    # Install zsh completion
    install -Dm644 completions/_t-pgsql "${pkgdir}/usr/share/zsh/site-functions/_t-pgsql"

    # Install bash completion
    install -Dm644 completions/t-pgsql.bash "${pkgdir}/usr/share/bash-completion/completions/t-pgsql"

    # Install man page
    install -Dm644 man/t-pgsql.1 "${pkgdir}/usr/share/man/man1/t-pgsql.1"

    # Install license
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Install documentation
    install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -Dm644 jobs.yaml.example "${pkgdir}/usr/share/doc/${pkgname}/jobs.yaml.example"
}
